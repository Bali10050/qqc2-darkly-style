if (BUILD_SHARED_LIBS)
    add_library(org.kde.darkly MODULE)
else()
    add_library(org.kde.darkly)
endif()

target_sources(org.kde.darkly PRIVATE
    plasmadesktoptheme.cpp
    kirigamiplasmafactory.cpp
    units.cpp
)

target_link_libraries(org.kde.darkly
    PUBLIC
        Qt::Core
        KF6::KirigamiPlatform
    PRIVATE
        Qt::Qml
        Qt::Quick
        KF6::GuiAddons
        KF6::ColorScheme
        KF6::IconThemes
)

if (HAVE_DBUS)
    target_link_libraries(org.kde.darkly
        PRIVATE
        Qt6::DBus
    )
    target_compile_definitions(org.kde.darkly PRIVATE -DHAVE_QTDBUS=1)
else()
    target_compile_definitions(org.kde.darkly PRIVATE -DHAVE_QTDBUS=0)
endif()

if(ANDROID)
    set_target_properties(org.kde.darkly PROPERTIES OUTPUT_NAME libplugins_kf6_kirigami_platform_org.kde.darkly)
endif()

if (NOT BUILD_SHARED_LIBS)
    target_compile_definitions(org.kde.darkly PRIVATE QT_STATICPLUGIN=1)
    set_target_properties(org.kde.darkly PROPERTIES QT_PLUGIN_CLASS_NAME KirigamiPlasmaFactory)
    __qt_internal_add_static_plugin_init_object_library(org.kde.darkly plugin_init_target)
    __qt_internal_propagate_object_library(org.kde.darkly "${plugin_init_target}")
endif()

install(TARGETS org.kde.darkly ${plugin_init_target} DESTINATION ${KDE_INSTALL_PLUGINDIR}/kf6/kirigami/platform EXPORT QQC2BreezeStyleTargets)

